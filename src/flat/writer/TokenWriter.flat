package flat/writer

import flat/ast
import flat/parser/matchers
import flat/log

import static flat/colorizer/Colorizer

abstract data class {
  static Logger log = Logger(Writer.class)

  visible TokenPattern pattern
  visible WriterOutputStream output

  var Bool _exhausted = false
  visible var Bool exhausted <=> _exhausted

  var Bool wasRepeat = false

  lazy Bool bareNodePattern => pattern.getNodeType() == Node.class

  lazy BufferWriterOutputStream buffer => BufferWriterOutputStream()

  abstract public write(WriterOutputStream output, Node node)

  public writeAll(WriterOutputStream output, Node[] nodes) {
    nodes.forEach({
      write(output, _)
    })
  }

  public writeAll(WriterOutputStream output, Node[] nodes, func()) {
    nodes.forEach({
      write(output, _)
    })
    func()
  }

  public writeStatement(WriterOutputStream output, Node node) {
    write(output, node)
    exhaust(output)
  }

  public writeStatement(WriterOutputStream output, Node[] nodes) {
    writeAll(output, nodes)
    exhaust(output)
  }

  public writeStatement(WriterOutputStream output, Node node, func()) {
    write(output, node)
    func()
    exhaust(output)
  }

  public writeStatement(WriterOutputStream output, Node[] nodes, func()) {
    writeAll(output, nodes)
    func()
    exhaust(output)
  }

  public exhaust(WriterOutputStream output) => true {
    log.infoFunc({"Ending current writer"})
  }

  public write(Node node) => write(output, node)
  public writeAll(Node[] nodes) => writeAll(output, nodes)
  public writeAll(Node[] nodes, func()) => writeAll(output, nodes, func)
  public writeStatement(Node node) => writeStatement(output, node)
  public writeStatement(Node[] nodes) => writeStatement(output, nodes)
  public writeStatement(Node node, func()) => writeStatement(output, node, func)
  public writeStatement(Node[] nodes, func()) => writeStatement(output, nodes, func)
  public exhaust() => exhaust(output)

  public validNode(Node node) => !bareNodePattern && node.class.isOfType(pattern.getNodeType())
  public validNodes(Node[] nodes) => nodes.all({ validNode(_) })

  public consumesNode() => !bareNodePattern
  public requiresNode() => !pattern.optional && consumesNode()
  public writePrimitiveTokens(WriterOutputStream output, Node node) => true {
    log.infoFunc({"Writing primitive Tokens #{magenta(pattern.toPatternString())}"})
    write(output, node)
  }

  public reset() {
    _exhausted = false
    wasRepeat = false

    buffer.clear()
  }
}